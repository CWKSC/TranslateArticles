---
date: 2020-06-30 19:00:00
layout: post
title: "CIS 194 02 Algebraic Data Types"
subtitle: 
description: 
image: https://cdn.jsdelivr.net/gh/CWKSC/MyResources/Common/pixel300x1_Transparent.png
optimized_image: 
category: CIS 194 spring 2013
tags:
  - CIS 194 spring 2013
  - Haskell
author: CWKSC
paginate: false
math: true
---

Source: [02-ADTs](https://www.seas.upenn.edu/~cis194/spring13/lectures/02-ADTs.html)

## ▌Algebraic Data Types 代數數據類型

CIS 194 第 2 週

2013 年 1 月 21 日

建議閱讀：

- [真實世界Haskell](http://book.realworldhaskell.org/)，第2章和第3章

## ▌枚舉類型

與許多編程語言一樣，Haskell 允許程序員創建自己的 *枚舉* 類型。這是一個簡單的例子：

```haskell
data Thing = Shoe 
           | Ship 
           | SealingWax 
           | Cabbage 
           | King
deriving Show
```

此聲明新的類型，稱為 `Thing` 具有五個 *數據構造*  `Shoe` ， `Ship` 等，這是 `Thing` 類型的值（唯一）。（這 `deriving Show` 是一個神奇的咒語，它告訴 GHC自動生成用於將 `Thing` s 轉換為 `String` s 的默認代碼。這是 `ghci` 在打印 type 表達式的值時使用的 `Thing` 。）

```haskell
shoe :: Thing
shoe = Shoe

listO'Things :: [Thing]
listO'Things = [Shoe, SealingWax, King, Cabbage, King]
```

我們可以 `Thing` 通過 *模式匹配* 在 s 上編寫函數。

```haskell
isSmall :: Thing -> Bool
isSmall Shoe       = True
isSmall Ship       = False
isSmall SealingWax = True
isSmall Cabbage    = True
isSmall King       = False
```

回顧一下如何按從上到下的順序嘗試函數子句，我們還可以使定義 `isSmall` 簡短一些，如下所示：

```haskell
isSmall2 :: Thing -> Bool
isSmall2 Ship = False
isSmall2 King = False
isSmall2 _    = True
```

## ▌除了枚舉

`Thing` 是 *枚舉類型* ，類似於其他語言（例如 Java 或 C++ ）提供的類型。但是，枚舉實際上只是 Haskell 更通用的 *代數數據類型* 的特例。作為不只是枚舉的數據類型的第一個示例，請考慮以下定義 `FailableDouble` ：

```haskell
data FailableDouble = Failure
                    | OK Double
deriving Show
```

這表示該 `FailableDouble` 類型具有兩個數據構造函數。第一個 `Failure` 沒有參數，因此 `Failure` 它本身就是 type 的值 `FailableDouble` 。第二個 `OK` 參數接受 type 的參數 `Double`。因此 `OK` 本身不是類型的值 `FailableDouble`; 我們需要給它一個 `Double` 。例如，`OK 3.4` 是 type 的值 `FailableDouble`。

```haskell
ex01 = Failure
ex02 = OK 3.4
```

思想練習：是什麼類型的 `OK`？

```haskell
safeDiv :: Double -> Double -> FailableDouble
safeDiv _ 0 = Failure
safeDiv x y = OK (x / y)
```

更多模式匹配！請注意，在這種`OK`情況下，我們如何命名隨`Double`其附帶的。

```haskell
failureToZero :: FailableDouble -> Double
failureToZero Failure = 0
failureToZero (OK d)  = d
```

數據構造函數可以有多個參數。

```haskell
-- Store a person's name, age, and favourite Thing.
data Person = Person String Int Thing
deriving Show

brent :: Person
brent = Person "Brent" 31 SealingWax

stan :: Person
stan  = Person "Stan" 94 Cabbage

getAge :: Person -> Int
getAge (Person _ a _) = a
```

請注意，類型構造函數和數據構造函數都是如何命名的 `Person` ，但是它們使用不同的名稱空間，並且是不同的東西。這種習慣用法（為一個構造函數的類型和數據構造函數賦予相同的名稱）是很常見的，但是在習慣之前會引起混淆。

## ▌一般的代數數據類型

通常，代數數據類型具有一個或多個數據構造函數，每個數據構造函數可以具有零個或多個參數。

```haskell
data AlgDataType = Constr1 Type11 Type12
                 | Constr2 Type21
                 | Constr3 Type31 Type32 Type33
                 | Constr4
```

這指定類型的值 `AlgDataType` 可以以四種方式之一來構造：使用 `Constr1` ， `Constr2` ， `Constr3`，或 `Constr4` 。根據所使用的構造函數，一個 `AlgDataType` 值可能包含其他一些值。例如，如果使用構造它 `Constr1`，則它帶有兩個值，一個是 type `Type11` ，另一個是 type `Type12` 。

最後一點：類型和數據構造函數名稱必須始終以大寫字母開頭；變量（包括函數名稱）必須始終以小寫字母開頭。（否則，Haskell 解析器將很難確定哪個名稱表示變量，哪個名稱表示構造函數）。

## ▌模式匹配

我們已經在一些特定情況下看到了模式匹配，但讓我們看看模式匹配通常是如何工作的。從根本上講，模式匹配是通過 *找出構建* 它的 *構造函數* 來分解一個值。這些信息可以用作決定做什麼的基礎-實際上，在 Haskell 中，這是做出決定的 *唯一* 方法。

例如，要決定如何處理 type 值 `AlgDataType`（在上一節中定義的虛構類型），我們可以編寫類似

```haskell
foo (Constr1 a b)   = ...
foo (Constr2 a)     = ...
foo (Constr3 a b c) = ...
foo Constr4         = ...
```

注意我們如何給每個構造函數附帶的值命名。還要注意，在不僅僅是一個構造函數的模式周圍需要括號。

這是模式背後的主要思想，但還有更多注意事項。

1. 下劃線 `_` 可以用作匹配任何內容的 “通配符模式”。

2. 形式的模式 `x@pat` 可用於將值與模式進行匹配 `pat`，但 *也*  `x` 可為整個要匹配的值指定名稱。例如：

   ```haskell
   baz :: Person -> String
   baz p@(Person n _ _) = "The name field of (" ++ show p ++ ") is " ++ n
   ```

   ```haskell
   *Main> baz brent
   "The name field of (Person \"Brent\" 31 SealingWax) is Brent"
   ```

3. 模式可以 *嵌套* 。例如：

   ```haskell
   checkFav :: Person -> String
   checkFav (Person n _ SealingWax) = n ++ ", you're my kind of person!"
   checkFav (Person n _ _)          = n ++ ", your favorite thing is lame."
   ```

   ```haskell
   *Main> checkFav brent
   "Brent, you're my kind of person!"
   *Main> checkFav stan
   "Stan, your favorite thing is lame."
   ```

   請注意我們如何將模式嵌套在的模式 `SealingWax` 內 `Person`。

通常，以下語法定義了可用作模式的內容：

```haskell
pat ::= _
     |  var
     |  var @ ( pat )
     |  ( Constructor pat1 pat2 ... patn )
```

第一行說下劃線是一種模式。第二行說變量本身就是一個模式：這種模式匹配任何東西，並將給定的變量名“綁定”到匹配的值。第三行指定 `@`-patterns。最後一行說，構造函數名稱後跟一系列模式本身就是一個模式：如果該模式是使用給定構造函數構造的，則該模式會與該值匹配，*並且*  `pat1` 通過 `patn` 遞歸方式與該構造函數包含的所有值進行匹配。

（實際上，模式的完整語法仍然包含更多功能，但是其餘的功能暫時將我們帶到了很遠的地方。）

請注意，像 `2` 或一樣的文字值 `'c'` 可以認為是沒有參數的構造函數。就像類型 `Int` 和 `Char` 定義一樣

```haskell
data Int  = 0 | 1 | -1 | 2 | -2 | ...
data Char = 'a' | 'b' | 'c' | ...
```

這意味著我們可以對文字值進行模式匹配。（當然，`Int` 並且 *實際上* `Char` 不是通過這種方式定義的。）

## ▌Case expressions 案例表達式

在 Haskell 中進行模式匹配的基本結構是 `case` 表達式。通常，`case` 表達式看起來像

```haskell
case exp of
  pat1 -> exp1
  pat2 -> exp2
  ...
```

當評估時，表達 `exp` 被針對每個圖案的匹配 `pat1`，`pat2` ... 反過來。選擇第一個匹配模式，然後整個 `case` 表達式計算為與匹配模式相對應的表達式。例如，

```haskell
ex03 = case "Hello" of
           []      -> 3
           ('H':s) -> length s
           _       -> 7
```

計算結果`4`（選擇了第二個模式；當然，第三個模式也匹配，但從未達到）。

實際上，我們看到的用於定義函數的語法實際上只是用於定義 `case` 表達式的便捷語法。例如， `failureToZero` 先前給定的定義可以等效地寫為

```haskell
failureToZero' :: FailableDouble -> Double
failureToZero' x = case x of
                     Failure -> 0
                     OK d    -> d
```

## ▌遞歸數據類型

數據類型可以是 *遞歸的* ，即根據自身定義。實際上，我們已經看到了遞歸類型-列表類型。列表為空，或為單個元素，後跟剩餘列表。我們可以這樣定義自己的列表類型：

```haskell
data IntList = Empty | Cons Int IntList
```

Haskell 自己的內置列表非常相似。他們只是使用特殊的內置語法（ `[]` 和 `:` ）。（當然，它們也適用於任何類型的元素，而不僅僅是 `Int`s；下週將繼續介紹。）

我們經常使用遞歸函數來處理遞歸數據類型：

```haskell
intListProd :: IntList -> Int
intListProd Empty      = 1
intListProd (Cons x l) = x * intListProd l
```

作為另一個簡單的示例，我們可以定義一種二叉樹的類型，其`Int`值存儲在每個內部節點上，而值`Char`存儲在每個葉上：

```haskell
data Tree = Leaf Char
          | Node Tree Int Tree
deriving Show
```

（不要問我您將把這樣的樹用於什麼；這是一個示例，好嗎？）例如，

```haskell
tree :: Tree
tree = Node (Leaf 'x') 1 (Node (Leaf 'y') 2 (Leaf 'z'))
```